buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.10'
		classpath 'org.jetbrains.kotlin:kotlin-serialization:1.4.10'
	}
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'application'

mainClassName = 'watch.dependency.Main'

dependencies {
	def okhttpVersion = '4.9.0'
	def kotlinxSerializationVersion = '1.0.0-RC2'
	def xmlUtilVersion = '0.80.0-RC'

	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
	implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:$kotlinxSerializationVersion"
	implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinxSerializationVersion"
	implementation 'com.charleskorn.kaml:kaml:0.23.0'
	implementation "net.devrieze:xmlutil-jvm:$xmlUtilVersion"
	implementation "net.devrieze:xmlutil-serialization-jvm:$xmlUtilVersion"
	implementation 'com.github.ajalt.clikt:clikt:3.0.1'
	implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
	implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

	testImplementation 'junit:junit:4.13'
	testImplementation 'com.google.truth:truth:1.0.1'
	testImplementation 'com.google.jimfs:jimfs:1.1'
	testImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
}

repositories {
	mavenCentral()
	jcenter()
}

tasks.named("distTar").configure { task ->
	task.enabled = false
}
tasks.named("assemble").configure { task ->
	task.dependsOn(tasks.getByName("installDist"))
}

tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
	task.kotlinOptions {
		jvmTarget = '1.8'
		languageVersion = '1.4'
		freeCompilerArgs += [
				'-progressive',
				'-XXLanguage:+NewInference',
				'-Xinline-classes',
				'-Xopt-in=kotlin.ExperimentalStdlibApi',
				'-Xopt-in=kotlin.ExperimentalUnsignedTypes',
				'-Xopt-in=kotlin.time.ExperimentalTime',
				'-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
				'-Xopt-in=kotlinx.coroutines.ObsoleteCoroutinesApi',
		]
	}
}
